#pragma kernel CSBlurHorizontal
#pragma kernel CSBlurVertical

#define DEF_ThreadCount 256 
#define DEF_BlurSigma 9

#define CacheSize DEF_ThreadCount + DEF_BlurSigma
groupshared float4 _gsCache[CacheSize];

Texture2D<float4>   _Source;
RWTexture2D<float4> _Output_Horizontal;
RWTexture2D<float4> _Output_Vertical;

StructuredBuffer<unorm float> _Weights;//This


[numthreads(DEF_ThreadCount,1,1)]
void CSBlurHorizontal   (uint3 groupID           : SV_GroupID, 
                         uint groupIndex         : SV_GroupIndex, 
                         uint3 groupThreadID     : SV_GroupThreadID, 
                         uint3 dispatchThreadId  : SV_DispatchThreadID)
{
    uint2 sourceSize;
    _Source.GetDimensions(sourceSize.x, sourceSize.y);
    int2 sourceUV = dispatchThreadId.xy;
    sourceUV.x = min(sourceUV.x, sourceSize.x -1);
    sourceUV.y = min(sourceUV.y, sourceSize.y -1);

    int blurRadius = (DEF_BlurSigma - 1) * 0.5f;
    _gsCache[groupThreadID.x + blurRadius] = _Source[sourceUV];
    if (groupThreadID.x < (uint)blurRadius) 
    {
        sourceUV.x = max(sourceUV.x - blurRadius, 0);    
        _gsCache[groupThreadID.x] = _Source[sourceUV]; 
    }
    else if (groupThreadID.x >= DEF_ThreadCount - (uint)blurRadius) 
    {
        sourceUV.x = min(sourceUV.x + blurRadius, sourceSize.x - 1); 
        _gsCache[groupThreadID.x + 2 * blurRadius] = _Source[sourceUV];
    }

    GroupMemoryBarrierWithGroupSync(); 

    float4 col = 0.0f;
    [unroll(DEF_BlurSigma)]
    for (int i = 0; i < DEF_BlurSigma; i++)
    {
        col += _gsCache[groupThreadID.x + i] * _Weights[i];
    }
   
    _Output_Horizontal[uint2(dispatchThreadId.x, dispatchThreadId.y)] = col;
}


[numthreads(1,DEF_ThreadCount,1)]
void CSBlurVertical     (uint3 groupID           : SV_GroupID, 
                         uint groupIndex         : SV_GroupIndex, 
                         uint3 groupThreadID     : SV_GroupThreadID, 
                         uint3 dispatchThreadId  : SV_DispatchThreadID)
{ 
    uint2 sourceSize;
    _Source.GetDimensions(sourceSize.x, sourceSize.y);
    int2 sourceUV = dispatchThreadId.xy;
    sourceUV.x = min(sourceUV.x, sourceSize.x -1);
    sourceUV.y = min(sourceUV.y, sourceSize.y -1);

     int blurRadius = (DEF_BlurSigma - 1) * 0.5f;
    _gsCache[groupThreadID.y + blurRadius] = _Source[sourceUV];
    if (groupThreadID.y < (uint)blurRadius) 
    {
        sourceUV.y = max(sourceUV.y - blurRadius, 0);    
        _gsCache[groupThreadID.y] = _Source[sourceUV]; 
    }
    else if (groupThreadID.y >= DEF_ThreadCount - (uint)blurRadius) 
    {
        sourceUV.y = min(sourceUV.y + blurRadius, sourceSize.y - 1); 
        _gsCache[groupThreadID.y + 2 * blurRadius] = _Source[sourceUV];
    }

    GroupMemoryBarrierWithGroupSync(); 

    float4 col = 0.0f;
    [unroll(DEF_BlurSigma)]
    for (int i = 0; i < DEF_BlurSigma; i++)
    {
        col += _gsCache[groupThreadID.y + i] * _Weights[i];
    }
    
    _Output_Vertical[uint2(dispatchThreadId.x, dispatchThreadId.y)] = col;
}




